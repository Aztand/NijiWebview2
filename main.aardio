import win.ui;
import web.view;
import web.rest.jsonLiteClient;
import web.json;
import wsock.tcp.asynHttpServer;
import console;
import string.chineseNumber;
import fsys;
import fsys.version;
import fsys.config;
import win.ui.minmax;
import process.ffmpeg;
import qrencode.bitmap;
import gdip.graphics;
import gdip.bitmap;
/*
import debug;
import debug.log;
debug.log.setPath("/.config/log.txt");
debug.log.checkSize(0x20000); //è®¾ç½®æ—¥å¿—å¤§å°
*/
/*DSG{{*/
mainForm = win.form(text="ä½ çš„æ—¥è®°";right=1480;bottom=1001;edge=1;maximize=1)
mainForm.add(
port={cls="static";text="close";left=56;top=936;right=120;bottom=961;hide=1;notify=1;transparent=1;z=1}
)
/*}}*/

win.ui.minmax(mainForm,1024, 768);   // é™åˆ¶å®½é«˜è°ƒæ•´

http = web.rest.jsonLiteClient();
wb = web.view(mainForm,{
        startArguments = {
            allowFileAccessFromFiles = true;    // å…è®¸è®¿é—®æœ¬åœ°æ–‡ä»¶
            disableWebSecurity = true;         // ç¦ç”¨å®‰å…¨ç­–ç•¥ï¼ˆå¼€å‘æ—¶ä½¿ç”¨ï¼‰
            remoteAllowOrigins = "*";          // å…è®¸æ‰€æœ‰è·¨åŸŸè¯·æ±‚
        };});
loginGo = "";
token = "";
uploadIP = "";
isMember = false;

NijiDocPath = io.getSpecial(0x05/*å³æ–‡æ¡£æ–‡ä»¶å¤¹*/) + "\ä½ çš„æ—¥è®°\";

thread.invoke(function(NijiDocPath,mainForm){	//çœ‹å›¾æœåŠ¡å™¨ã€‚
	import wsock.tcp.simpleHttpServer;
	var imgServer = wsock.tcp.simpleHttpServer();
	var threadIp;
	var threadPort;
	_,mainForm.port.text = imgServer.getLocalIp();//ä¼šreturn ip,portä¸¤ä¸ªè¿”å›å€¼ï¼Œæ‰€ä»¥ç”¨ä¸‹åˆ’çº¿å ä½ipã€‚
	imgServer.documentRoot = NijiDocPath
	
	imgServer.run( 
	    function(response,request,session){ 
			//æ·»åŠ ç¦æ­¢ç¼“å­˜çš„HTTPå¤´
			response.headers["Cache-Control"] = "no-cache, no-store, must-revalidate";
			response.headers["Pragma"] = "no-cache";
			response.headers["Expires"] = "0";
			
	   		response.loadcode( request.path );	

	    }
	);
	
},NijiDocPath,mainForm);

uploadTmpTable = {};	//å›¾ç‰‡åˆ—è¡¨
uploadTmpNum = 1;	//ç¼“å­˜å›¾ç‰‡ç¼–å·
uploadServer = wsock.tcp.asynHttpServer();

uploadServer.run( {

	//è‡ªå®šä¹‰æŸä¸ªè·¯å¾„çš„å“åº”ç¨‹åº
	["/upload/main.aardio"] = function(response,request,session){ 
			//æ·»åŠ ç¦æ­¢ç¼“å­˜çš„HTTPå¤´
			response.headers["Cache-Control"] = "no-cache, no-store, must-revalidate";
			response.headers["Pragma"] = "no-cache";
			response.headers["Expires"] = "0";
			
			fileData = request.postFileData()
			//console.open()
			if(fileData){
				
				var outputDir = io.joinpath(NijiDocPath, "upload_temp");
				io.createDir(outputDir);
				
				var fileName = io.joinpath(NijiDocPath,"upload_temp",uploadTmpNum + '.jpg') 
				var image_object = gdip.bitmap(fileData.file.value());
				if(image_object){
					// å‹å›¾ï¼é˜²æ­¢ä½ è®°æœåŠ¡å™¨çˆ†ç‚¸

					temp_buffer = image_object.saveToBuffer();
					while( #temp_buffer > 1000000){
						image_object = image_object.resize( image_object.width * 0.9, image_object.height * 0.9) ;
						temp_buffer = image_object.saveToBuffer();
					}
					image_object.save(fileName);
					response.contentType = "text/plain";
					response.write(uploadTmpNum);
					wb.invoke('addUploadPreview',uploadTmpNum);	//æ·»åŠ é¢„è§ˆ
					table.insert(uploadTmpTable,uploadTmpNum) ;	//æ·»åŠ è¡¨ã€‚ç”¨äºä¸Šä¼ ã€‚
					uploadTmpNum++ ;
				}
				else {
					response.error(err) 
				}
				

				return response.close() 
			} 		
    }
    
    ["/mobileUpload/main.aardio"] = function(response,request,session){
		//æ·»åŠ ç¦æ­¢ç¼“å­˜çš„HTTPå¤´
		response.headers["Cache-Control"] = "no-cache, no-store, must-revalidate";
		response.headers["Pragma"] = "no-cache";
		response.headers["Expires"] = "0";
    	
    	response.loadcode("/res/web/mobileUpload.html");
    }
    
    
    //è¡¨é‡Œæ‰¾ä¸åˆ°çš„è·¯å¾„ï¼Œä»ç„¶ä¼šæ­£å¸¸è®¿é—®å­˜åœ¨çš„æ–‡ä»¶ï¼ˆæ”¯æŒèµ„æºæ–‡ä»¶ï¼‰	
} );
uploadIP = uploadServer.getUrl( , true);
_,uploadPort = uploadServer.getLocalIp();	//å¿«ä¼ äºŒç»´ç 
QRcodeURL = uploadIP + "/mobileUpload/";
QRobj = qrencode.bitmap( QRcodeURL );
QRobj.copyBitmap( 512 ).save( io.joinpath(NijiDocPath,"upload_temp","QRcode.jpg") , 512 )


zh = string.chineseNumber(); 
daytoweek = function(inputnum){
	weekDic = {"å¤©","ä¸€","äºŒ","ä¸‰","å››","äº”","å…­"};
	outputweek = weekDic[inputnum + 1];
	return outputweek; 
}

addSelfCard = function(tableIndex){
	
	//æ£€æµ‹åˆ—è¡¨æ˜¯å¦éœ€è¦æ·»åŠ æ–°æœˆä»½
	if(time(diaryResponse["diaries"][tableIndex]['createddate'],"%Y-%m-%d").month != nowMonth){
		diaryCreatedDate = time(diaryResponse["diaries"][tableIndex]['createddate'],"%Y-%m-%d");
		nowMonth = diaryCreatedDate.month;
		wb.invoke("addMonthText",{
		year = diaryCreatedDate.year;
		month = diaryCreatedDate.month;
		monthStr = zh.number(nowMonth) + "æœˆ";
		});
	}
	
	//åˆ›å»ºæ—¥è®°åˆ—è¡¨å¡ç‰‡
	cardDay = time(diaryResponse["diaries"][tableIndex]['createddate'],"%Y-%m-%d").day;
	cardWeek = "æ˜ŸæœŸ" + daytoweek(time(diaryResponse["diaries"][tableIndex]['createddate'],"%Y-%m-%d").dayofweek);
	cardWriteTime = tostring(time(diaryResponse["diaries"][tableIndex]['ts']),"%H:%M");
	if(diaryResponse["diaries"][tableIndex]['title'] != ""){
       	cardTitle = diaryResponse["diaries"][tableIndex]['title']; 
	}
	else {
   		cardTitle = diaryResponse["diaries"][tableIndex]['createddate'];
	}
	
	//å¡ç‰‡ä¸Šæ—¥è®°é¢„è§ˆæ–‡å­—
	if(diaryResponse["diaries"][tableIndex]['content'] != "")
	{
	    cardSimpleStrip = string.replace(diaryResponse["diaries"][tableIndex]['content'], "\s+", " ");
	
	    if(string.len(cardSimpleStrip)>19)
   		    cardSimple = cardSimpleStrip + "â€¦"
	    else
    	    cardSimple = cardSimpleStrip;
    }
    	
    cardStyle = 'card-self';
    	
	cardOwner = 'self';
	cardDiaryId = diaryResponse["diaries"][tableIndex]['id'];
	createdDate = diaryResponse["diaries"][tableIndex]['createddate'];
	cardReadmark = 0;
	if(diaryResponse["read_marks"][tostring(cardDiaryId)] != null)
		cardReadmark = diaryResponse["read_marks"][tostring(cardDiaryId)];

	wb.invoke("addDiaryCard",{
    	cardDay = cardDay;
    	cardWeek = cardWeek;
    	cardWriteTime = cardWriteTime;
    	cardTitle = cardTitle;
    	cardSimple = cardSimple;
    	cardStyle = cardStyle;
    	cardOwner = cardOwner;
    	cardUserID = userID;
    	cardDiaryId = cardDiaryId;
    	createdDate = createdDate;
    	cardReadmark = cardReadmark
	});
}

addPairedCard = function(tableIndex){
	
	//æ£€æµ‹åˆ—è¡¨æ˜¯å¦éœ€è¦æ·»åŠ æ–°æœˆä»½
	if(time(diaryResponse["diaries_paired"][tableIndex]['createddate'],"%Y-%m-%d").month != nowMonth){
		
		diaryCreatedDate = time(diaryResponse["diaries_paired"][tableIndex]['createddate'],"%Y-%m-%d");
		nowMonth = diaryCreatedDate.month;
		wb.invoke("addMonthText",{
		year = diaryCreatedDate.year;
		month = diaryCreatedDate.month;
		monthStr = zh.number(nowMonth) + "æœˆ";
		});
	}
	
	//åˆ›å»ºæ—¥è®°åˆ—è¡¨å¡ç‰‡
	cardDay = time(diaryResponse["diaries_paired"][tableIndex]['createddate'],"%Y-%m-%d").day;
	cardWeek = "æ˜ŸæœŸ" + daytoweek(time(diaryResponse["diaries_paired"][tableIndex]['createddate'],"%Y-%m-%d").dayofweek);
	cardWriteTime = tostring(time(diaryResponse["diaries_paired"][tableIndex]['ts']),"%H:%M");
	if(diaryResponse["diaries_paired"][tableIndex]['title'] != ""){
       	cardTitle = diaryResponse["diaries_paired"][tableIndex]['title']; 
	}
	else {
   		cardTitle = diaryResponse["diaries_paired"][tableIndex]['createddate'];
	}
	
	//å¡ç‰‡ä¸Šæ—¥è®°é¢„è§ˆæ–‡å­—
	if(diaryResponse["diaries_paired"][tableIndex]['content'] != "")
	{
	    cardSimpleStrip = string.replace(diaryResponse["diaries_paired"][tableIndex]['content'], "\s+", " ");
	
	    if(string.len(cardSimpleStrip)>19)
   		    cardSimple = cardSimpleStrip + "â€¦"
	    else
    	    cardSimple = cardSimpleStrip;
    }
    	
    cardStyle = 'card-paired';
    	
	cardOwner = 'paired';
	cardDiaryId = diaryResponse["diaries_paired"][tableIndex]['id'];
	createdDate = diaryResponse["diaries_paired"][tableIndex]['createddate'];
	cardReadmark = 0;
	wb.invoke("addDiaryCard",{
    	cardDay = cardDay;
    	cardWeek = cardWeek;
    	cardWriteTime = cardWriteTime;
    	cardTitle = cardTitle;
    	cardSimple = cardSimple;
    	cardStyle = cardStyle;
    	cardOwner = cardOwner;
    	cardUserID = pairedID;
    	cardDiaryId = cardDiaryId;
    	createdDate = createdDate;
    	cardReadmark = cardReadmark
	});
}

v2sync = function(){
	diaryResponse = http.api("https://nideriji.cn/api/v2/sync/").post(
    		user_config_ts = 0,
    		diaries_ts = 0,
    		readmark_ts = 0,
    		images_ts = 0
    );
    
    isMember = diaryResponse["user_config"]["is_member"];
    uploadTmpTable = {};	//æ¸…ç©ºä¸Šä¼ å›¾ç‰‡ç›¸å…³çš„ç¼“å­˜
	uploadTmpNum = 1;
	//å› ä¸ºå›¾ç‰‡ä¸‹è½½å¯ä»¥å¼€å¯å¤šçº¿ç¨‹åå°ï¼Œæ‰€ä»¥ç›´æ¥å…ˆå¼€å§‹æ£€æµ‹ä¸‹è½½å†å¤„ç†æ—¥è®°å¡ç‰‡ã€æ—¥è®°åˆ—è¡¨ã€‚
	if(isMember){	//åªä¸ºproç”¨æˆ·ä¸‹è½½å›¾ç‰‡ï¼ŒèŠ‚çº¦æœåŠ¡å™¨å¸¦å®½ã€‚
	
		
		thread.invoke( 		//ä¸ºè‡ªå·±ä¸‹å›¾
			function( diaryResponse,NijiDocPath,token,wb ){
				import web.rest.jsonLiteClient;
				http = web.rest.client();
				http.addHeaders = {
					["auth"] = "token " ++ token;
					["user-agent"] = "OhApp/3.6.12 Platform/Android";
				}
				
				userConfig = diaryResponse["user_config"];
				
				if(diaryResponse["images"] != null && table.len(diaryResponse["images"]) != 0){
					picTable = diaryResponse["images"];
					picTotal = picTable[table.len(picTable)].image_id;//é€šè¿‡æœ€åä¸€å¼ å›¾idåˆ¤æ–­å›¾ç‰‡æ€»æ•°ã€‚
					
					for(i=picTotal;1;-1){
						if(!io.exist(NijiDocPath ++ userConfig.userid ++ "\" ++ i ++ ".jpg")){
							picApi = http.api("https://f.nideriji.cn/api/image/" ++ userConfig.userid ++ "/" ++ i ++ "/");
							pic = picApi.get();
							if( pic ){//é˜²æ­¢è¯¥å›¾ä¸å­˜åœ¨ï¼Œä¸ºç©ºç¼–å·
								string.save(NijiDocPath ++ userConfig.userid ++ "\" ++ i ++ ".jpg", pic )
							}
						}	
					}
					//ä¸ºå‰ç«¯å›¾åº“æ·»åŠ å›¾ç‰‡
					for(i=1;picTotal;1){
						if(io.exist(NijiDocPath ++ userConfig.userid ++ "\" ++ i ++ ".jpg")){
							wb.invoke("addPicture",i);
						}	
					}
				}
				
			},diaryResponse,NijiDocPath,token,wb
		);
		
		if( diaryResponse["user_config"]["paired"] ){//ä¸‹è½½å¯¹é¢å›¾åƒã€‚
			thread.invoke(
				function( diaryResponse,NijiDocPath,token ){
					import web.rest.jsonLiteClient;
					http = web.rest.client();
					http.addHeaders = {
						["auth"] = "token " ++ token;
						["user-agent"] = "OhApp/3.6.12 Platform/Android";
					}
					pairedConfig = diaryResponse["user_config"]["paired_user_config"];
					
					if(diaryResponse["images_paired"] != null && table.len(diaryResponse["images_paired"]) != 0){
						picTable = diaryResponse["images_paired"];
						picTotal = picTable[table.len(picTable)].image_id;//é€šè¿‡æœ€åä¸€å¼ å›¾idåˆ¤æ–­å›¾ç‰‡æ€»æ•°ã€‚
						
						for(i=picTotal;1;-1){
							if(!io.exist(NijiDocPath ++ pairedConfig.userid ++ "\" ++ i ++ ".jpg")){
								picApi = http.api("https://f.nideriji.cn/api/image/" ++ pairedConfig.userid ++ "/" ++ i ++ "/");
								pic = picApi.get();
								if( pic ){//é˜²æ­¢è¯¥å›¾ä¸å­˜åœ¨ï¼Œä¸ºç©ºç¼–å·
									string.save(NijiDocPath ++ pairedConfig.userid ++ "\" ++ i ++ ".jpg", pic )
								}
							}	
						}
					}
					
				},diaryResponse,NijiDocPath,token
			);
		}
	}
		
	userID = diaryResponse["user_config"]["userid"];	//å› ä¸ºæ·»åŠ æ—¥è®°å¡ç‰‡å¯èƒ½éœ€è¦å‡ ç§’é’Ÿï¼Œæ‰€ä»¥å¯ä»¥æŠŠå¿«çš„å°å±æ€§éƒ½å…ˆè®¾ç½®å¥½ã€‚
	wb.invoke('setUserId' , userID);	//htmlæ–¹é¢å…¨å±€å˜é‡ï¼Œä¸»è¦ç”¨äºæ–°å»ºç©ºç™½æ—¥è®°ä»¥åŠå›¾ç‰‡ç´¢å¼•ã€‚åŒ¹é…å¯¹è±¡çš„idåœ¨ä¸‹é¢è®¾ç½®ã€‚
	
	//ä¸ºhtmlä¼ è¾“çœ‹å›¾ç«¯å£
	if(mainForm.port.text != "close"){
		wb.invoke('setPort' , mainForm.port.text ); 
	}
	
	wb.invoke('setUploadPort', uploadPort );	
	wb.invoke('setUserColor' , diaryResponse["user_config"]["role"] , diaryResponse["user_config"]["paired_user_config"]["role"] || null);	//ä¸»é¢˜è‰²ï¼ŒåŒ…æ‹¬ä¸‰ä¸ªæ‚¬æµ®æŒ‰é’®ä»¥åŠç®€ä»‹ç”¨æˆ·å
	var pairedInfo = "";
	//â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”åªæœ‰åŒ¹é…çŠ¶å†µæ˜¯æƒ…å†µæ¯”è¾ƒå¤šï¼Œéœ€è¦ä¸“é—¨è®¡ç®—ä¸€ä¸‹ã€‚åˆ«çš„ç¨å¾®é“¾æ¥å­—ç¬¦ä¸²å°±å¯ä»¥åŠ å…¥å³ä¸Šè§’ä¸ªäººä¿¡æ¯å¡ç‰‡
	if(diaryResponse["user_config"]["paired"]){
		if(time.now().diffday(time(diaryResponse["user_config"]["paired_user_config"]["last_login_time"])) == 0){
			if(time.now().diffhour(time(diaryResponse["user_config"]["paired_user_config"]["last_login_time"])) == 0){
				pairedInfo = "ğŸ™ å¯¹é¢åœ¨" + time.now().diffminute(time(diaryResponse["user_config"]["paired_user_config"]["last_login_time"])) + "åˆ†é’Ÿå‰ç™»å½•è¿‡~";
			}
			else {
				pairedInfo = "ğŸ™ å¯¹é¢åœ¨" + time.now().diffhour(time(diaryResponse["user_config"]["paired_user_config"]["last_login_time"])) + "å°æ—¶å‰ç™»å½•è¿‡~";
			}
		}
		else {
			pairedInfo = "ğŸ™ å¯¹é¢åœ¨" + time.now().diffday(time(diaryResponse["user_config"]["paired_user_config"]["last_login_time"])) + "å¤©å‰ç™»å½•è¿‡~";
		}
	}
	elseif(diaryResponse["user_config"]["random_pair_joined"]){
		pairedInfo = "ğŸ™ åŒ¹é…å°†åœ¨æ™šä¸Šå…«ç‚¹å®Œæˆ~";
	}
	elseif(diaryResponse["user_config"]["pair_requested"]){
		pairedInfo = "ğŸ™ ä½ çš„å®šå‘è¯·æ±‚å·²å‘å‡º";	
	}
	else {
		pairedInfo = "ğŸ™ ä½ è¿˜æ²¡æœ‰åŠ å…¥ä»»ä½•é…å¯¹";
	}
	//â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
	var nameInfo = diaryResponse["user_config"]["name"] ? diaryResponse["user_config"]["name"] : "ä½ çš„åå­—" ;//ä¸‰å…ƒè¿ç®—ç¬¦
	var descInfo =  diaryResponse["user_config"]["description"] ?  diaryResponse["user_config"]["description"] : "å»æ‰‹æœºç«¯è®¾ç½®ä¸€æ¡æ ¼è¨€å§";
	
	wb.invoke('setUserInfoCard', nameInfo, "âœ’ï¸ " + descInfo, pairedInfo, 
		"ğŸ“ " + diaryResponse["user_config"]["diary_count"] + "ç¯‡ | " + diaryResponse["user_config"]["word_count"] + "å­— | " + diaryResponse["user_config"]["image_count"] + "å›¾", diaryResponse["user_config"]["is_member"]);
	if(diaryResponse["user_config"]["avatar"]){
		wb.invoke("setAvatar","https://f.nideriji.cn" + diaryResponse["user_config"]["avatar"]);
	}
	
	i = 0;
	j = 0;
	iLen = diaryResponse["user_config"]["diary_count"];
	jLen = 0;
	iDate = time("1900-01-01","%Y-%m-%d");
	jDate = time("1900-01-01","%Y-%m-%d");
	nowMonth = 0;
	if(iLen){
		i = 1
		iDate = time(diaryResponse["diaries"][i]["createddate"],"%Y-%m-%d");
	}
	
	if( diaryResponse["user_config"]["paired"] ){		//é˜²æ­¢ç”¨æˆ·æœªè¿æ¥è™«æ´
		pairedConfig = diaryResponse["user_config"]["paired_user_config"]
		pairedID = pairedConfig["userid"];
		wb.invoke('setPairedId', pairedID);				//è®¾ç½®ç½‘é¡µPairedId
		//wb.invoke('setPairedGender', pairedConfig["role"]);
		wb.invoke('setPairSpan', pairedConfig["role"], pairedConfig["name"]);
		jLen = table.len(diaryResponse["diaries_paired"])
		if(jLen){
			j = 1
			jDate = time(diaryResponse["diaries_paired"][j]["createddate"],"%Y-%m-%d");	
		}
		
	}
	
	while(i <= iLen && iLen){
		addSelfCard(i);
		i++;
	}
	while(j <= jLen && jLen){
		addPairedCard(j);
		j++;
	}
	
}

wb.external = {
	login = function(email,password,remPw){
		loginResponse = http.api("https://nideriji.cn/api/login/").post(
			email = email,
			password = password
		);
		token = loginResponse.token;
		
		if(!loginResponse){
			wb.doScript('document.getElementById("wrong-tip").hidden = false;');
		}
		elseif(loginResponse["error"] != 0) {
			wb.doScript('document.getElementById("wrong-tip").hidden = false;');
		}
		else{
			/*ç”¨äºåœ¨å¼€å‘é˜¶æ®µè·å–è‡ªå·±çš„token
			console.log(loginResponse.token)
			//*/
			
			loginGo = 1;
			http.addHeaders = {
				["auth"] = "token " ++ loginResponse.token;
    			["user-agent"] = "OhApp/3.6.12 Platform/Android";
    		}
    		if(remPw){
    		    cfg.token.token = loginResponse.token;
    		}
		}	
	};	
	
	logout = function(){
		loginGo = 2
		cfg.token.token = ""
	}
	
	loadPage = function(){
		v2sync();
	}
	
	
	msgbox = function(msg){
		mainForm.msgbox(msg);
	}
	

	writeDiary = function(dateText,titleText,contentText){
		saveResponse = http.api("https://nideriji.cn/api/write/").post(
			title = titleText,
			content = contentText,
			date = dateText
		);
		
		var returnTable = {
			status = "",
			newDiaryId = "",
			createdDate = "",
			}
			
		if(saveResponse){
			returnTable.status = "Success";
			returnTable.newDiaryId = saveResponse["diary"]["id"];
			returnTable.createdDate = saveResponse["diary"]["createddate"];
			
			return web.json.stringify( returnTable ); 
		}
		else {
			returnTable.status = "error"; 
			return web.json.stringify( returnTable );
		}
	}
	
	deleteDiary = function(userID,diaryID){
		delResponse = http.api("https://nideriji.cn/api/diary/delete/"++ diaryID ++ "/").get();
		if(delResponse["error"] != null ? delResponse["error"] == 0 : false/* è‹¥ä¸å­˜åœ¨delRåˆ™ç›´æ¥æŠ¥é”™ */){
			return "Success"; 	
		}
		else {
			return "error"; 
		}
	}
	
	getDiary = function(ownerID,diaryID){
		getAllResponse = http.api("https://nideriji.cn/api/diary/all_by_ids/" ++ ownerID ++ "/").post(
			diary_ids = diaryID
		);
		if(getAllResponse["diaries"][1]){
			http.api("https://nideriji.cn/api/update_read_mark/" ++ diaryID ++ "/").get();
			return web.json.stringify(getAllResponse["diaries"][1],true);
		}
		else {
			return "error";
		} 
		
	}
	
	uploadImage = function(/* è‡ªåŠ¨è¯»å–UploadTmpTableä»¥åŠupload_tempä¸‹æ–‡ä»¶ï¼Œæ‰€ä»¥æ— éœ€å‚æ•° */){
		
		if(!isMember){
			mainForm.msgbox("è¯·å¼€é€šä¼šå‘˜åä½¿ç”¨å›¾ç‰‡åŠŸèƒ½ï¼");
			return 0; 
		}
		
		for(k,v in uploadTmpTable){
			uploadApi = http.api("https://f.nideriji.cn/api/upload_image/");
			uploadResponse = uploadApi.sendMultipartForm( { 
				image = "@" + NijiDocPath + "\upload_temp\" + v + ".jpg";
			});
			if(uploadResponse["error"] != null){
				if(uploadResponse["error"] == 0){
					newPicId = uploadResponse["image_id"];
					if(!io.exist(io.joinpath( NijiDocPath , userID )))
						io.createDir(io.joinpath(NijiDocPath,userID));
					fsys.move(io.joinpath(NijiDocPath,"upload_temp", v + ".jpg"),io.joinpath(NijiDocPath,userID,newPicId + ".jpg"));
					wb.invoke("addPicture",newPicId);
					wb.invoke("delUploadPreview",v);
				}
				else {
					wb.invoke("alert","ä¸Šä¼ å‡ºé”™ï¼");
				}
				
			}
		}
		
		uploadTmpTable = {};//é‡ç½®åˆå§‹åŒ–ã€‚
		uploadTmpNum = 1;
	}
	
	removePreview = function(previewNum){
		previewNum = tonumber(previewNum);
		
		table.removeByValue(uploadTmpTable,previewNum);
		io.remove(io.joinpath(NijiDocPath,"upload_temp", previewNum + ".jpg"));
		
	}
	
	unpair = function(){
		var unpairResp = http.api("https://nideriji.cn/api/unpair/").get();
		if(unpairResp["error"] != null){
			if(unpairResp["error"] == 0 ){
				return "success"; 	
			}
		}
		return "error"; 
	}
	
	joinRandomPair = function(){
		var joinResp = http.api("https://nideriji.cn/api/join_random_pair/").get();
		if(joinResp["error"] != null){
			if(joinResp["error"] == 0 ){
				return "success"; 	
			}
		}
		return "error"; 
	}
	
	cancelRandomPair = function(){
		var cancelResp = http.api("https://nideriji.cn/api/cancel_random_pair/").get();
		if(cancelResp["error"] != null){
			if(cancelResp["error"] == 0 ){
				return "success"; 	
			}
		}
		return "error"; 
	}
	
	knowYourName = function(){
		var knowResp = http.api("https://nideriji.cn/api/know_your_name/").get();
		if(knowResp["error"] != null){
			if(knowResp["error"] == 0 ){
				return "success"; 	
			}
		}
		return "error"; 
	}
	
	
	directPair = function(targetEmail){
		var dircResp = http.api("https://nideriji.cn/api/pair/").post(
			target_email = targetEmail
		)
		if(dircResp["error"] != null){
			if(dircResp["error"] == 0 ){
				if(dircResp["paired"]){
					return "paired"; 
				}
				else{
					return "waiting"; 
				}
			}
		}
		return "error"; 
	}
	
	
};//wb.externalç»“æŸ


loginIntvalID = mainForm.setInterval( 
	function(){
		if(loginGo == 1){
			wb.go("/res/web/index.html");
			loginGo = 0;
		}
		elseif (loginGo == 2)  {
			wb.go("/res/web/login.html");
			loginGo = 0;
		}
		
	},200
)



mainForm.onClose = function(hwnd,message,wParam,lParam){
    if(wb.xcall('hasUnsavedChanges')){
    	if(!mainForm.msgboxTest("å½“å‰æ—¥è®°æœ‰æœªä¿å­˜å†…å®¹ï¼Œæ˜¯å¦ç¡®è®¤å…³é—­çª—å£ï¼Ÿ"))
    		return "ä¸å…³ä¸å…³å°±ä¸å…³";    
    }
}


wb.go("/res/web/login.html");
wb.focus();
mainForm.show();

//è‡ªåŠ¨ç™»å½•ç»„ä»¶
cfgPath = io.appData("/nideriji");
cfg = fsys.config(cfgPath)	//åˆ›å»ºconfigå¯¹è±¡

if(table.count(cfg.token) == 2){			//ç¬¬ä¸€ä¸ªtokenæ˜¯æ–‡ä»¶åï¼Œç¬¬äºŒä¸ªtokenæ˜¯å­—æ®µåã€‚ç©ºçš„configæ–‡ä»¶countä¸º1
	if(cfg.token.token != ""){
		token = cfg.token.token ;
		http.addHeaders = {
			["auth"] = "token " + cfg.token.token ;
			["user-agent"] = "OhApp/3.6.12 Platform/Android";
		}
		testToken = http.api("https://nideriji.cn/api/v2/sync/").post();
		if(testToken){
			loginGo = 1;	
		}
		else {
			cfg.token.token = "";
			mainForm.msgbox("å…ˆå‰ä¿å­˜çš„ç™»å½•å‡­æ®å·²å¤±æ•ˆ");
		}
	}
}

//*
versionObj = fsys.version.getInfo(io._exepath);		//ç‰ˆæœ¬æ›´æ–°ç›¸å…³
currentVersion = string.join({versionObj.productVersion.major; versionObj.productVersion.minor; versionObj.productVersion.build;}, ".")
newVersion = http.api("http://frp-fit.com:57258/").get();
if(newVersion && newVersion > currentVersion ){
	var versionForm = mainForm.loadForm("\res\newVersion.aardio");
	versionForm.edit.text = "æ£€æµ‹åˆ°ä½ çš„æ—¥è®°PCç‰ˆæœ‰æ›´æ–°ï¼Œæœ€æ–°ç‰ˆä¸ºï¼š" + newVersion ++ '\r\n' ++ "ä¸‹è½½é“¾æ¥ï¼šhttps://aztand.lanzoub.com/b00wsr3wd å¯†ç ï¼šnideriji";
	versionForm.show();
}
//*/


return win.loopMessage();